10 REM THIS PROGRAM PLAYS THE SONG "IEVAN POLKKA" USING THE YM2151 CHIP.


100 REM READ THE ENTIRE SONG INTO THREE ARRAYS
110 DIM NO(100): REM NOTES OF THE MELODY
120 DIM DU(100): REM DURATION OF EACH NOTE
130 DIM CH(100): REM CHORD
140 I=0
150 READ NO(I), DU(I), CH(I)
160 IF NO(I)=$FF THEN GOTO 200
170 I=I+1
180 GOTO 150

200 REM INITIALIZE
210 GOSUB 2000: REM INITIALIZE SOUND CHIP
220 SO=TI: REM GET START TIME
230 K=0: REM INITIALIZE ARRAY POINTER

300 REM MAIN LOOP
310 GOSUB 1000
320 GOTO 310

1000 REM UPDATE SOUND
1010 IF TI<SO THEN RETURN: REM WAIT UNTIL NOTE HAS ENDED
1020 POKE $9FE0, $08: POKE $9FE1, $00: REM KEY OFF
1030 IF NO(K)>0 THEN GOSUB 1200
1040 IF CH(K)>0 THEN GOSUB 1400
1050 SO=SO+DU(K)*8: REM CALCULATE WHEN TO END NOTE
1060 K=K+1
1070 IF K=I THEN K=0
1080 RETURN

1200 POKE $9FE0, $28: POKE $9FE1, NO(K):  REM SET KEY CODE
1210 POKE $9FE0, $08: POKE $9FE1, $08: REM KEY ON
1220 RETURN

1400 POKE $9FE0, $08: POKE $9FE1, $01: REM KEY OFF
1410 POKE $9FE0, $08: POKE $9FE1, $02: REM KEY OFF
1420 POKE $9FE0, $08: POKE $9FE1, $03: REM KEY OFF
1430 POKE $9FE0, $29: POKE $9FE1, CH(K):  REM SET KEY CODE
1440 IF CH(K)=$41 THEN GOSUB 1600: REM D MINOR
1450 IF CH(K)=$3E THEN GOSUB 1700: REM C MAJOR
1460 POKE $9FE0, $08: POKE $9FE1, $09: REM KEY ON
1470 POKE $9FE0, $08: POKE $9FE1, $0A: REM KEY ON
1480 POKE $9FE0, $08: POKE $9FE1, $0B: REM KEY ON
1490 RETURN

1600 POKE $9FE0, $2A: POKE $9FE1, $45:  REM SET KEY CODE
1610 POKE $9FE0, $2B: POKE $9FE1, $4A:  REM SET KEY CODE
1620 RETURN

1700 POKE $9FE0, $2A: POKE $9FE1, $44:  REM SET KEY CODE
1710 POKE $9FE0, $2B: POKE $9FE1, $48:  REM SET KEY CODE
1720 RETURN

2000 POKE $9FE0, $80: POKE $9FE1, $1F: REM CHANNEL 0 MODULATOR 1 ATTACK RATE
2005 POKE $9FE0, $A0: POKE $9FE1, $0B: REM CHANNEL 0 MODULATOR 1 DECAY RATE
2010 POKE $9FE0, $E0: POKE $9FE1, $FF: REM CHANNEL 0 MODULATOR 1 RELEASE RATE
2020 POKE $9FE0, $20: POKE $9FE1, $E7: REM CHANNEL 0 FEEDBACK AND CONNECTION

2100 POKE $9FE0, $61: POKE $9FE1, $08: REM CHANNEL 1 MODULATOR 1 TOTAL LEVEL
2102 POKE $9FE0, $81: POKE $9FE1, $1F: REM CHANNEL 1 MODULATOR 1 ATTACK RATE
2105 POKE $9FE0, $A1: POKE $9FE1, $07: REM CHANNEL 1 MODULATOR 1 DECAY RATE
2110 POKE $9FE0, $E1: POKE $9FE1, $FF: REM CHANNEL 1 MODULATOR 1 RELEASE RATE
2120 POKE $9FE0, $21: POKE $9FE1, $D7: REM CHANNEL 1 FEEDBACK AND CONNECTION

2200 POKE $9FE0, $62: POKE $9FE1, $08: REM CHANNEL 2 MODULATOR 1 TOTAL LEVEL
2202 POKE $9FE0, $82: POKE $9FE1, $1F: REM CHANNEL 2 MODULATOR 1 ATTACK RATE
2205 POKE $9FE0, $A2: POKE $9FE1, $07: REM CHANNEL 2 MODULATOR 1 DECAY RATE
2210 POKE $9FE0, $E2: POKE $9FE1, $FF: REM CHANNEL 2 MODULATOR 1 RELEASE RATE
2220 POKE $9FE0, $22: POKE $9FE1, $D7: REM CHANNEL 2 FEEDBACK AND CONNECTION

2300 POKE $9FE0, $63: POKE $9FE1, $08: REM CHANNEL 3 MODULATOR 1 TOTAL LEVEL
2302 POKE $9FE0, $83: POKE $9FE1, $1F: REM CHANNEL 3 MODULATOR 1 ATTACK RATE
2305 POKE $9FE0, $A3: POKE $9FE1, $07: REM CHANNEL 3 MODULATOR 1 DECAY RATE
2310 POKE $9FE0, $E3: POKE $9FE1, $FF: REM CHANNEL 3 MODULATOR 1 RELEASE RATE
2320 POKE $9FE0, $23: POKE $9FE1, $D7: REM CHANNEL 3 FEEDBACK AND CONNECTION

2999 RETURN

3010 DATA $51, 1, 0: REM D

3100 DATA $4A, 2, $41: REM A + D MINOR
3110 DATA $51, 2, 0: REM D
3120 DATA $51, 3, 0: REM D
3130 DATA $54, 1, 0: REM E

3200 DATA $55, 2, $41: REM F + D MINOR
3210 DATA $51, 2, 0: REM D
3220 DATA $51, 2, 0: REM D
3230 DATA $55, 1, 0: REM F
3235 DATA $55, 1, 0: REM F

3300 DATA $54, 2, $3E: REM E + C MAJOR
3310 DATA $4E, 2, 0: REM C
3320 DATA $4E, 2, 0: REM C
3330 DATA $54, 2, 0: REM E

3400 DATA $55, 2, $41: REM F + D MINOR
3410 DATA $51, 2, 0: REM D
3420 DATA $51, 3, 0: REM D
3430 DATA $51, 1, 0: REM D

3500 DATA $4A, 2, $41: REM A + D MINOR
3510 DATA $51, 2, 0: REM D
3520 DATA $51, 3, 0: REM D
3530 DATA $54, 1, 0: REM E

3600 DATA $55, 2, $41: REM F + D MINOR
3610 DATA $51, 2, 0: REM D
3620 DATA $51, 2, 0: REM D
3630 DATA $55, 1, 0: REM F
3635 DATA $55, 1, 0: REM F

3700 DATA $5A, 2, $3E: REM A + C MAJOR
3710 DATA $58, 2, 0: REM G
3720 DATA $55, 2, 0: REM F
3730 DATA $54, 2, 0: REM E

3800 DATA $55, 2, $41: REM F + D MINOR
3810 DATA $51, 2, 0: REM D
3820 DATA $51, 3, 0: REM D
3830 DATA $51, 1, 0: REM D

4000 DATA $5A, 2, $41: REM A + D MINOR
4010 DATA $5A, 2, 0: REM A
4020 DATA $58, 2, 0: REM G
4030 DATA $55, 2, 0: REM F

4100 DATA $58, 2, $3E: REM G + C MAJOR
4110 DATA $54, 2, 0: REM E
4120 DATA $54, 3, 0: REM E
4130 DATA $54, 1, 0: REM E

4200 DATA $58, 2, $3E: REM G + C MAJOR
4210 DATA $58, 2, 0: REM G
4220 DATA $55, 2, 0: REM F
4230 DATA $54, 2, 0: REM E

4300 DATA $55, 2, $41: REM F + D MINOR
4310 DATA $51, 2, 0: REM D
4320 DATA $51, 3, 0: REM D
4330 DATA $51, 1, 0: REM D

4400 DATA $5A, 2, $41: REM A + D MINOR
4410 DATA $5A, 2, 0: REM A
4420 DATA $58, 2, 0: REM G
4430 DATA $55, 2, 0: REM F

4500 DATA $58, 2, $3E: REM G + C MAJOR
4510 DATA $54, 2, 0: REM E
4520 DATA $54, 3, 0: REM E
4530 DATA $54, 1, 0: REM E

4600 DATA $58, 1, $3E: REM G + C MAJOR
4605 DATA $58, 1, 0: REM G
4610 DATA $58, 2, 0: REM G
4620 DATA $55, 2, 0: REM F
4630 DATA $54, 2, 0: REM E

4700 DATA $51, 4, $41: REM D + D MINOR
4710 DATA $51, 3, 0: REM D

9999 DATA $FF, 1, 0: REM END

